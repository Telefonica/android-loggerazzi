apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {
        maven {
            credentials {
                username System.env.NEXUS_USER
                password System.env.NEXUS_PASS
            }
            url "https://nexusng.tuenti.io/repository/maven-release-private/"
        }
    }
    publications {
        gradlePlugin(MavenPublication) {
            groupId 'com.telefonica'
            artifactId 'loggerazzi-gradle-plugin'
            version version

            artifact("$buildDir/libs/gradle-plugin-${version}.jar")

            pom {
                name = 'Android Loggerazzi Gradle Plugin'
                description = 'Gradle Plugin for logs snapshot testing for Android Instrumentation tests.'
                url = 'https://github.com/Telefonica/android-loggerazzi'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'android-team-telefonica'
                        name = 'Android Team'
                        email = 'cto-android@telefonica.com'
                    }
                }
                scm {
                    connection = 'scm:git:github.com/Telefonica/android-loggerazzi.git'
                    developerConnection = 'scm:git:ssh://github.com/Telefonica/android-loggerazzi.git'
                    url = 'https://github.com/Telefonica/android-loggerazzi/tree/main'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.getByName("implementation").dependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

afterEvaluate {
    tasks.getByName("publishGradlePluginPublicationToMavenLocal").dependsOn("jar")
    tasks.getByName("publishGradlePluginPublicationToSonatypeRepository").dependsOn("jar")
    tasks.getByName("publishGradlePluginPublicationToMavenRepository").dependsOn("jar")
    tasks.getByName("signGradlePluginPublication").dependsOn("jar")
    tasks.getByName("signPluginMavenPublication").dependsOn("jar")
}


signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications
}
